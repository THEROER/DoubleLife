plugins {
    id 'java-library'
    id 'io.github.goooler.shadow' version '8.1.7'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'dev.ua.theroer'
version = '1.0.1'
description = 'DoubleLife standalone plugin'

def magicutilsVersion = '1.9.1'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly 'net.luckperms:api:5.4'

    implementation "dev.ua.theroer:magicutils-bukkit:${magicutilsVersion}"
    implementation "dev.ua.theroer:magicutils-config-yaml:${magicutilsVersion}"
    annotationProcessor "dev.ua.theroer:magicutils-processor:${magicutilsVersion}"
    implementation 'com.google.code.gson:gson:2.10.1'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-parameters']
}

tasks.processResources {
    def props = [version: version, description: description]
    inputs.properties(props)
    filteringCharset = 'UTF-8'
    filesMatching(['plugin.yml']) {
        expand(props)
    }
}

tasks.shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    relocate 'com.fasterxml.jackson', 'dev.ua.theroer.doublelife.libs.jackson'
}

tasks.jar {
    archiveClassifier.set('plain')
}

tasks.build.dependsOn tasks.shadowJar

tasks {
    runServer {
        dependsOn tasks.shadowJar
        pluginJars(tasks.shadowJar.archiveFile)
        minecraftVersion("1.21.8")
        downloadPlugins {
            modrinth("luckperms", "v5.5.17-bukkit")
            modrinth("placeholderapi", "2.11.7")
        }
    }
}
